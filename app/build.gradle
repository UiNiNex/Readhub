apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'walle.gradle'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    defaultConfig {
        applicationId APP_ID
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode project.gitCommitCount
        versionName project.gitTag
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //只保留对应的国际化
        resConfigs "zh", "en"
        buildConfigField "String", "BUGLY", '"b8676c281b"'
        multiDexEnabled true
        multiDexKeepProguard file('multiDexKeep.pro')
    }
    dexOptions {
        jumboMode = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    signingConfigs {
        release {
            storeFile file("../release.jks")
            storePassword gradle.password
            keyAlias gradle.keyAlias
            keyPassword gradle.password
        }
        debug {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        release {
            shrinkResources true //default true
            minifyEnabled true //default true
            signingConfig signingConfigs.release
            debuggable false //default false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "IS_DEBUG", "false" //default false
            ndk {
                //设置支持的SO库架构
                abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
            }
        }
        debug {
            shrinkResources false
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "IS_DEBUG", "true" //default false
        }
    }


    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        abortOnError false
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.isDebuggable()) {
                return
            }
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "readhub_${project.gitTag}.${project.gitCommitCount}.apk"
                outputFileName = fileName
            }
        }
    }
}


def junitVersion = "4.12"
def mockitoVersion = "2.13.0"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:multidex:1.0.3" // 多dex配置

    //    implementation 'com.google.firebase:firebase-core:15.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    provided 'javax.annotation:jsr250-api:1.0'

    implementation "com.android.support:appcompat-v7:${SUPPORT_V7_VERSION}"
    implementation "com.android.support:design:${SUPPORT_V7_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_V7_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_V7_VERSION}"
    implementation "com.android.support:support-v4:${SUPPORT_V7_VERSION}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    kapt "com.android.databinding:compiler:${gradle_version}"
    implementation "com.google.dagger:dagger:${dagger_version}"
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    implementation 'com.lovejjfg.powerrecycle:powerrecycle:1.2.7'
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    kapt 'com.github.bumptech.glide:compiler:4.7.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.7.1'
    implementation 'org.jsoup:jsoup:1.11.3'
    testImplementation 'junit:junit:4.12'
    //bugly
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.5'
    implementation 'com.tencent.tinker:tinker-android-lib:1.9.9'

    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.lovejjfg.powertext:lib:0.0.5'
    implementation 'androidx.core:core-ktx:0.3'
    implementation 'com.meituan.android.walle:library:1.1.6'

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
}

apply plugin: 'com.google.gms.google-services'

// 依赖插件脚本
apply from: 'tinker-support.gradle'
